#!/usr/bin/kermit +

; Copyright 2023 John Goerzen

set input timeout quit
set exit warning off
set input echo on

; Write out, after a delay that is absolutely necessary for the emulator
define WOUTLINE {
   mpause 200
   lineout \%1
}

; Write out, after a delay that is absolutely necessary.
define WOUT {
   mpause 200
   output \%1
}

; Wait for an input
define WIN5 {
   input 5 \%1
   if fail exit 1 "Failed waiting for \%1"
} 

; Wait for an input
define WIN {
   input \%1 \%2
   if fail exit 1 "Failed waiting for \%1"
} 

define WPROMPT {
   WIN5 "# "
}

define WTRANS {
   set input echo off
   echo "Transmitting \%1..."
   transmit /noecho /text \%1
   echo "Transmission complete."
   set input echo on
} 

cd ..
set host /net:pseudo pdp11 unixv7.simh
if fail exit 1 "Failed to start emulator"

win5 "Disabling XQ"
woutline "boot"

win5 "Boot"
win5 ": "

woutline "hp(0,0)munix"
win 20 "# "

; Ctrl-D
wout \4
win5 "login: "
woutline root

win5 Password:
woutline root

wprompt
woutline "cd /usr/src"

wprompt
woutline "mkdir uue"

wprompt
woutline "cd uue"

wprompt
woutline "cat > uudecode.c"
mpause 200
wtrans /opt/vint/downloads/uudecode.c
; Ctrl-D
wout \4

win 35 "# "
woutline "cc -O -o uudecode uudecode.c && echo good"

win 10 "good"
wprompt

woutline "cat > uuencode.c"
mpause 200
wtrans /opt/vint/downloads/uuencode.c
; Ctrl-D
wout \4

wprompt
woutline "cc -O -o uuencode uuencode.c && echo good"

win 10 "good"
wprompt

woutline "strip uudecode"
wprompt

woutline "strip uuencode"
wprompt

woutline "cp uudecode uuencode /bin"
wprompt

woutline "cd /usr"
wprompt

woutline "uudecode"
mpause 200
wtrans ../usr-src-ucb.tar.uue

win 120 "# "
woutline "cd /"
wprompt
woutline "tar -xvf /usr/usr-src-ucb.tar"
win 20 "# "
woutline "rm /usr/usr-src-ucb.tar"

wprompt
woutline "cd /usr/src/ucb/compress"

wprompt
woutline "cp compress /bin"

wprompt
woutline "cd /bin"

wprompt
woutline "ln compress uncompress"

wprompt
woutline "ln compress zcat"

wprompt

woutline "sync"
wprompt

woutline "sync"
wprompt

woutline "sync"
wprompt

wout \5
win5 "sim> "
woutline "q"

exit 0 all done
